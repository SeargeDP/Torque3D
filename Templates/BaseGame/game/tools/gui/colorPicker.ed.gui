//--- OBJECT WRITE BEGIN ---
$guiContent = new GuiColorPickerCtrl(ColorPickerDlg,EditorGuiGroup) {
      displayMode = "Dropper";
      extent = "1024 768";
      profile = "GuiDefaultProfile";
      command = "%selHue = ColorPickerDlg.getSelectedHue();\n%selSat = ColorPickerDlg.getSelectedSaturation();\n%selBright = ColorPickerDlg.getSelectedBrightness();\n%selAlpha = ColorPickerDlg.getSelectedAlpha();\n\nColorHueRange.setSelectedHue(%selHue);\nColorHueRange.executeUpdate();\n\nColorBlendRange.setSelectedBrightness(%selBright);\nColorBlendRange.setSelectedSaturation(%selSat);\nColorBlendRange.executeUpdate();\n\nColorAlphaRange.setSelectedAlpha(%selAlpha);\nColorAlphaRange.executeUpdate();\n\nColorEyeDropperButton.setStateOn(false);";
      tooltipProfile = "GuiToolTipProfile";
      isContainer = "1";

   new GuiWindowCtrl() {
      text = "  ";
      resizeWidth = "0";
      resizeHeight = "0";
      canMinimize = "0";
      canMaximize = "0";
      closeCommand = "DoColorPickerCancelCallback(); ColorPickerDlg.getRoot().popDialog(ColorPickerDlg);";
      position = "33 33";
      extent = "271 574";
      horizSizing = "windowRelative";
      vertSizing = "windowRelative";
      profile = "ToolsGuiWindowProfile";
      tooltipProfile = "ToolsGuiToolTipProfile";

      new GuiStackControl() {
         padding = "5";
         changeChildSizeToFit = "0";
         changeChildPosition = "0";
         position = "0 24";
         extent = "271 491";
         profile = "GuiDefaultProfile";
         tooltipProfile = "GuiToolTipProfile";

         new GuiRolloutCtrl() {
            caption = ":: Color Picker";
            extent = "271 440";
            horizSizing = "width";
            profile = "GuiRolloutProfile";
            tooltipProfile = "GuiToolTipProfile";

            new GuiStackControl() {
               padding = "1";
               changeChildSizeToFit = "0";
               changeChildPosition = "0";
               position = "0 17";
               extent = "271 420";
               profile = "GuiDefaultProfile";
               tooltipProfile = "GuiToolTipProfile";

               new GuiControl() {
                  extent = "267 260";
                  profile = "GuiDefaultProfile";
                  tooltipProfile = "GuiToolTipProfile";
                  isContainer = "1";

                  new GuiColorPickerCtrl(ColorBlendRange) {
                     selectorGap = "8";
                     displayMode = "BlendRange";
                     position = "7 0";
                     extent = "255 255";
                     profile = "GuiDefaultProfile";
                     command = "\n%selSat = ColorBlendRange.getSelectedSaturation();\n%selBright = ColorBlendRange.getSelectedBrightness();\n\nColorNewSelected.setSelectedBrightness(%selBright);\nColorNewSelected.setSelectedSaturation(%selSat);\nColorNewSelected.executeUpdate();";
                     tooltipProfile = "GuiToolTipProfile";
                  };
               };
               new GuiControl() {
                  position = "0 261";
                  extent = "267 62";
                  profile = "GuiDefaultProfile";
                  tooltipProfile = "GuiToolTipProfile";
                  isContainer = "1";

                  new GuiBitmapCtrl() {
                     BitmapAsset = "ToolsModule:gridTiny2_image";
                     color = "125 125 125 255";
                     wrap = "1";
                     position = "7 32";
                     extent = "199 19";
                     profile = "ToolsGuiDefaultProfile";
                     tooltipProfile = "GuiToolTipProfile";
                  };
                  new GuiBitmapCtrl() {
                     BitmapAsset = "ToolsModule:gridTiny2_image";
                     color = "125 125 125 255";
                     wrap = "1";
                     position = "213 3";
                     extent = "49 49";
                     profile = "ToolsGuiDefaultProfile";
                     tooltipProfile = "GuiToolTipProfile";
                  };
                  new GuiColorPickerCtrl(ColorHueRange) {
                     selectorGap = "4";
                     displayMode = "HueRange";
                     position = "7 4";
                     extent = "200 20";
                     profile = "GuiDefaultProfile";
                     command = "%hueSel = ColorHueRange.getSelectedHue();\nColorBlendRange.setSelectedHue(%hueSel);\nColorAlphaRange.setSelectedHue(%hueSel );\n\nColorNewSelected.setSelectedHue(%hueSel);\nColorNewSelected.executeUpdate();";
                     tooltipProfile = "GuiToolTipProfile";
                  };
                  new GuiColorPickerCtrl(ColorAlphaRange) {
                     selectorGap = "4";
                     displayMode = "AlphaRange";
                     position = "7 32";
                     extent = "200 20";
                     profile = "GuiDefaultProfile";
                     command = "ColorNewSelected.setSelectedAlpha(ColorAlphaRange.getSelectedAlpha());\nColorNewSelected.executeUpdate();";
                     tooltipProfile = "GuiToolTipProfile";
                  };
                  new GuiColorPickerCtrl(ColorNewSelected) {
                     position = "213 3";
                     extent = "50 25";
                     profile = "GuiDefaultProfile";
                     command = "%selHue = ColorNewSelected.getSelectedHue();\n%selSat = ColorNewSelected.getSelectedSaturation();\n%selBright = ColorNewSelected.getSelectedBrightness();\n\nHueTextEditor.setText(%selHue);\nSatTextEditor.setText(%selSat);\nBrightTextEditor.setText(%selBright);\n\n%color = $ColorCallbackType == 1 ? ColorNewSelected.getSelectedColorI() : ColorNewSelected.getSelectedLinearColor();\nRedTextEdit.setText(getWord(%color, 0));\nGreenTextEdit.setText(getWord(%color, 1));\nBlueTextEdit.setText(getWord(%color, 2));\nAlphaTextEdit.setText(getWord(%color, 3));\n\n%hex = ColorRGBToHEX(ColorNewSelected.getSelectedColorI());\nHexTextEditor.setText(%hex);";
                     tooltipProfile = "GuiToolTipProfile";
                  };
                  new GuiColorPickerCtrl(ColorOld) {
                     position = "213 27";
                     extent = "50 25";
                     profile = "GuiDefaultProfile";
                     command = "%selHue = ColorOld.getSelectedHue();\n%selSat = ColorOld.getSelectedSaturation();\n%selBright = ColorOld.getSelectedBrightness();\n%selAlpha = ColorOld.getSelectedAlpha();\n\nColorHueRange.setSelectedHue(%selHue);\nColorHueRange.executeUpdate();\n\nColorBlendRange.setSelectedBrightness(%selBright);\nColorBlendRange.setSelectedSaturation(%selSat);\nColorBlendRange.executeUpdate();\n\nColorAlphaRange.setSelectedAlpha(%selAlpha);\nColorAlphaRange.executeUpdate();";
                     tooltipProfile = "GuiToolTipProfile";
                  };
               };
               new GuiControl() {
                  position = "0 324";
                  extent = "267 96";
                  profile = "GuiDefaultProfile";
                  tooltipProfile = "GuiToolTipProfile";
                  isContainer = "1";

                  new GuiControl() {
                     extent = "98 104";
                     profile = "GuiDefaultProfile";
                     tooltipProfile = "GuiToolTipProfile";
                     isContainer = "1";

                     new GuiTextCtrl() {
                        text = "R:";
                        position = "8 0";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(RedTextEdit) {
                        position = "30 1";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                        class = "ColorPickerRGBClass";
                     };
                     new GuiTextCtrl() {
                        text = "G:";
                        position = "8 24";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(GreenTextEdit) {
                        position = "30 24";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                        class = "ColorPickerRGBClass";
                     };
                     new GuiTextCtrl() {
                        text = "B:";
                        position = "8 48";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(BlueTextEdit) {
                        position = "30 48";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                        class = "ColorPickerRGBClass";
                     };
                     new GuiTextCtrl() {
                        text = "A:";
                        position = "8 72";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(AlphaTextEdit) {
                        position = "30 72";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                        class = "ColorPickerRGBClass";
                     };
                  };
                  new GuiControl() {
                     position = "112 0";
                     extent = "97 104";
                     profile = "GuiDefaultProfile";
                     tooltipProfile = "GuiToolTipProfile";
                     isContainer = "1";

                     new GuiTextCtrl() {
                        text = "H:";
                        position = "8 0";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(HueTextEditor) {
                        position = "30 1";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                        class = "ColorPickerHSBClass";
                     };
                     new GuiTextCtrl() {
                        text = "S:";
                        position = "8 24";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(SatTextEditor) {
                        position = "30 24";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                        class = "ColorPickerHSBClass";
                     };
                     new GuiTextCtrl() {
                        text = "B:";
                        position = "8 48";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(BrightTextEditor) {
                        position = "30 48";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                        class = "ColorPickerHSBClass";
                     };
                     new GuiTextCtrl() {
                        text = "#";
                        position = "8 72";
                        extent = "13 21";
                        profile = "ToolsGuiTextProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                     new GuiTextEditCtrl(HexTextEditor) {
                        position = "30 72";
                        extent = "64 20";
                        profile = "ToolsGuiTextEditProfile";
                        tooltipProfile = "GuiToolTipProfile";
                     };
                  };
                  new GuiButtonCtrl() {
                     text = "Apply";
                     position = "211 1";
                     extent = "52 20";
                     profile = "ToolsGuiButtonProfile";
                     command = "DoColorPickerCallback();";
                     tooltipProfile = "GuiToolTipProfile";
                  };
                  new GuiButtonCtrl() {
                     text = "Cancel";
                     position = "211 24";
                     extent = "52 20";
                     profile = "ToolsGuiButtonProfile";
                     command = "DoColorPickerCancelCallback();";
                     tooltipProfile = "GuiToolTipProfile";
                  };
                  new GuiBitmapButtonCtrl(ColorEyeDropperButton) {
                     BitmapAsset = "ToolsModule:eyedropper_n_image";
                     buttonType = "ToggleButton";
                     position = "223 56";
                     extent = "25 25";
                     profile = "ToolsGuiCheckBoxProfile";
                     command = "ColorPickerDlg.activateEyeDropper();";
                     tooltipProfile = "GuiToolTipProfile";
                  };
               };
            };
         };
         new GuiRolloutCtrl() {
            caption = ":: Color Palette";
            margin = "5 5 5 5";
            position = "0 445";
            extent = "271 46";
            profile = "GuiRolloutProfile";
            tooltipProfile = "GuiToolTipProfile";

            new GuiDynamicCtrlArrayControl(ColorPaletteStack) {
                  colCount = "12";
                  colSize = "12";
                  rowSize = "12";
                  rowCount = "3";
                  rowSpacing = "1";
                  colSpacing = "1";
                  dynamicSize = "1";
                  padding = "3 3 3 3";
                  position = "0 17";
                  extent = "271 47";
                  profile = "GuiDefaultProfile";
                  tooltipProfile = "GuiToolTipProfile";
               };
         };
      };
   };
};
//--- OBJECT WRITE END ---

$ColorPickerCallback = ""; // Control that we need to update
$ColorPickerCancelCallback = "";
$ColorPickerUpdateCallback = "";
$ColorCallbackType   = 1;  // ColorI

// This function pushes the color picker dialog and returns to a callback the selected value
function GetColorI( %currentColor, %callback, %root, %updateCallback, %cancelCallback )
{
   $ColorPickerSignal = 1; 
   $ColorPickerCallback = %callback;
   $ColorPickerCancelCallback = %cancelCallback;
   $ColorPickerUpdateCallback = %updateCallback;
   $ColorCallbackType = 1; // ColorI
   
   ColorOld.setSelectedColorI(%currentColor);
   ColorOld.executeUpdate();
   
   if(!isObject(%root))
      %root = Canvas;
  
   %root.pushDialog(ColorPickerDlg);
}

function GetColorF( %currentColor, %callback, %root, %updateCallback, %cancelCallback )
{
   $ColorPickerSignal = 1; 
   $ColorPickerCallback = %callback;
   $ColorPickerCancelCallback = %cancelCallback;
   $ColorPickerUpdateCallback = %updateCallback;
   $ColorCallbackType = 2; // ColorF
   
   ColorOld.setSelectedLinearColor(%currentColor);
   ColorOld.executeUpdate();
   
   if(!isObject(%root))
      %root = Canvas;
   %root.pushDialog(ColorPickerDlg);
}

function ColorPaletteStack::onWake(%this)
{
   if($Pref::ColorPicker::ColorPalette $= "")
   {
      $Pref::ColorPicker::ColorPalette = "#FFFFFF #FF0000 #00FF00 #0000FF";
   }

   %colorCount = getWordCount($Pref::ColorPicker::ColorPalette);
   
   if(%colorCount > 63)
      %colorCount = 63;

   for(%i=0; %i < %colorCount; %i++)
   {
      %hex = getWord($Pref::ColorPicker::ColorPalette, %i);
      %rgb = ColorHEXToRGB(%hex);
      %rgb = ColorIntToFloat(%rgb);

      %colorBox = new GuiSwatchButtonCtrl() {
                  extent = "16 16";
                  color = %rgb;
                  profile = "GuiDefaultProfile";
                  colorHex = %hex;
                  useMouseEvents = "1";
                  class = "ColorPaletteSwatch";
               };

      ColorPaletteStack.Add(%colorBox);
   }

   %button = new GuiButtonCtrl() {
               text = "+";
               extent = "16 16";
               profile = "ToolsGuiButtonProfile";
               tooltipProfile = "GuiToolTipProfile";
               command = "ColorPaletteStack.addCurrentColorToStack();";
            };

   ColorPaletteStack.Add(%button);
}

function ColorPaletteStack::addCurrentColorToStack(%this)
{
   %hex = HexTextEditor.getValue();
   //Make sure we have 6 characters
   while(strlen(%hex) < 6)
   {
      %hex = "0" @ %hex;
   }
   %hex = strupr(%hex);

   $Pref::ColorPicker::ColorPalette = "#" @ %hex SPC $Pref::ColorPicker::ColorPalette;
   ColorPaletteStack.clear();
   ColorPaletteStack.onWake();
}

function ColorPaletteStack::onSleep(%this)
{
   ColorPaletteStack.clear();
}

function ColorPaletteSwatch::onMouseDown(%this)
{
   //Update all the other color fields
   %rgb = ColorHEXToRGB(%this.colorHex);
   %hsb = ColorRGBToHSB(%rgb);
   
   // these automatically update our ColorNewSelected which
   // updates all text fields including these.
   ColorHueRange.setSelectedHue(getWord(%hsb, 0));
   ColorHueRange.executeUpdate();

   ColorBlendRange.setSelectedSaturation(getWord(%hsb, 1));
   ColorBlendRange.setSelectedBrightness(getWord(%hsb, 2));
   ColorBlendRange.executeUpdate();

   // for now just set full alpha until we save out alpha as well
   ColorAlphaRange.setSelectedAlpha(255);
   ColorAlphaRange.executeUpdate();
}

function ColorPickerRGBClass::onValidate(%this)
{
   %red = RedTextEdit.getValue();
   %green = GreenTextEdit.getValue();
   %blue = BlueTextEdit.getValue();
   %alpha = AlphaTextEdit.getValue();

   //Update all the other color fields
   if( $ColorCallbackType == 1)
      %hsb = ColorRGBToHSB(%red SPC %green SPC %blue);
   else
   {
      %hsb = ColorLinearRGBToHSB(%red SPC %green SPC %blue);
      %alpha *= 255.0;
   }
   // these automatically update our ColorNewSelected which
   // updates all text fields including these.
   ColorHueRange.setSelectedHue(getWord(%hsb, 0));
   ColorHueRange.executeUpdate();

   ColorBlendRange.setSelectedSaturation(getWord(%hsb, 1));
   ColorBlendRange.setSelectedBrightness(getWord(%hsb, 2));
   ColorBlendRange.executeUpdate();

   ColorAlphaRange.setSelectedAlpha(%alpha);
   ColorAlphaRange.executeUpdate();
}

function ColorPickerHSBClass::onValidate(%this)
{
   %hue = HueTextEditor.getValue();
   %saturation = SatTextEditor.getValue();
   %brightness = BrightTextEditor.getValue();

   // these automatically update our ColorNewSelected which
   // updates all text fields including these.
   ColorHueRange.setSelectedHue(%hue);
   ColorHueRange.executeUpdate();

   ColorBlendRange.setSelectedSaturation(%saturation);
   ColorBlendRange.setSelectedBrightness(%brightness);
   ColorBlendRange.executeUpdate();
}

function HexTextEditor::onKeyDown(%this)
{
   //Get the value
   %value = %this.getValue();
   
   //It's hex so keep it all uppercase
   %value = strupr(%value);
   %pos = %this.getCursorPos();
   %this.setValue(%value);
   %this.setCursorPos(%pos);
   
   //Verify that it's a hex value
   %value = stripChars(%value, "0123456789ABCDEF");
   if(%value $= "")
   {
      %this.validText();
   }
   else
   {
      %this.invalidText(false);
   }
}

function HexTextEditor::onValidate(%this)
{   
   //if the current text is invalid don't do anyting
   if(!%this.isValidText())
   {
      %this.invalidText(true);
      return;
   }
   
   //Get the current value
   %hex = %this.getValue();
   
   //Make sure we have 6 characters
   while(strlen(%hex) < 6)
   {
      %hex = "0" @ %hex;
   }
   %hex = strupr(%hex);
   
   //Update the value in case there were missing characters
   %this.setValue(%hex);
   
   //Update all the other color fields
   %rgb = ColorHEXToRGB(%hex);
   %hsb = ColorRGBToHSB(%rgb);
   
   // these automatically update our ColorNewSelected which
   // updates all text fields including these.
   ColorHueRange.setSelectedHue(getWord(%hsb, 0));
   ColorHueRange.executeUpdate();

   ColorBlendRange.setSelectedSaturation(getWord(%hsb, 1));
   ColorBlendRange.setSelectedBrightness(getWord(%hsb, 2));
   ColorBlendRange.executeUpdate();
}

// return mycolor.color
function DoColorPickerCallback()
{
   if( $ColorCallbackType == 1)
      %col = ColorNewSelected.getSelectedColorI();
   else
      %col = ColorNewSelected.getSelectedLinearColor();

   eval( $ColorPickerCallback @ "(\"" @ %col @"\");" );
   ColorPickerDlg.getRoot().popDialog(ColorPickerDlg);   
}   

function DoColorPickerCancelCallback()
{
   if( $ColorCallbackType == 1)
      %col = ColorOld.getSelectedColorI();
   else
      %col = ColorOld.getSelectedLinearColor();

   ColorPickerDlg.getRoot().popDialog( ColorPickerDlg );
   if( $ColorPickerCancelCallback !$= "" )
      eval( $ColorPickerCancelCallback @ "(\"" @ %col @ "\");" );
}

function DoColorPickerUpdateCallback()
{
   if( $ColorCallbackType == 1)
      %col = ColorNewSelected.getSelectedColorI();
   else
      %col = ColorNewSelected.getSelectedLinearColor();

   if( $ColorPickerUpdateCallback !$= "" )
      eval( $ColorPickerUpdateCallback @ "(\"" @ %col @ "\");" );
}
